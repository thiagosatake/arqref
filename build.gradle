plugins {
    id 'base'
    id "io.spring.dependency-management" version "1.1.5"
}

description = 'Architecture Reference'

ext {
	moduleProjects = subprojects.findAll{it.name.startsWith("arqref-one-")}
	javaProjects = subprojects.findAll { !it.name.startsWith("arqref-bom") }
}

configure(allprojects){ project ->

	apply plugin: "maven-publish"

	group = "arqref.one"
	version = "1.0.0-SNAPSHOT"
		
	repositories {
        mavenLocal()
        mavenCentral()
    }
    
}

configure(moduleProjects){ project ->	
	apply plugin: "java-library"
	
	java {
    	withJavadocJar()
    	withSourcesJar()
	}
	
	publishing {
	    publications {
	        maven(MavenPublication) {
	         	from components.java	
	            versionMapping {
	                usage('java-api') {
	                    fromResolutionOf('runtimeClasspath')
	                }
	                usage('java-runtime') {
	                    fromResolutionResult()
	                }
	            }
	        }
	    }
	}
	
}


configure(javaProjects) { project ->
	
	apply plugin: 'java'
	apply plugin: "io.spring.dependency-management"
	
	java {
		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21 	
	}

	compileJava.options.encoding = 'UTF-8'
	compileTestJava.options.encoding = 'UTF-8'
	
	dependencyManagement {
	     imports {
	          mavenBom 'org.springframework.boot:spring-boot-dependencies:3.3.1'
	     }
	}
	
	dependencies{
	 	compileOnly 'org.projectlombok:lombok'
  		annotationProcessor 'org.projectlombok:lombok'
  	}

}








